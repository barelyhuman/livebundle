buildscript {
  ext.versions = [
    // Android
    'minSdk'      : 21,
    'compileSdk'  : 29,
    'targetSdk'   : 29,
    // Libraries
    'appCompat'   : '1.1.0',
    'cameraxAplha': '1.0.0-alpha14',
    'cameraxBeta' : '1.0.0-beta07',
    'kotlin'      : '1.3.72',
    'mlkit'       : '16.0.0'
  ]

  ext.deps = [
    'appCompat'           : "androidx.appcompat:appcompat:${versions.appCompat}",
    'cameraxCore'         : "androidx.camera:camera-core:${versions.cameraxBeta}",
    'cameraxCamera2'      : "androidx.camera:camera-camera2:${versions.cameraxBeta}",
    'cameraxView'         : "androidx.camera:camera-view:${versions.cameraxAplha}",
    'cameraxLifecycle'    : "androidx.camera:camera-lifecycle:${versions.cameraxBeta}",
    'kotlinStdlib'        : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
    'mlkitBarcodeScanning': "com.google.mlkit:barcode-scanning:${versions.mlkit}"
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }

  repositories {
    google()
    jcenter()
  }
}


allprojects {
  group = 'com.walmartlabs.livebundle'
  project.ext.artifactId = project.name
  version = '0.1.8-SNAPSHOT'
  project.ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url "$rootDir/../node_modules/react-native/android"
    }
    maven {
      // Android JSC is installed from npm
      url "$rootDir/../node_modules/jsc-android/dist"
    }
    mavenCentral()
    google()
    jcenter()
  }
}

// Configure projects build options
subprojects { project ->
  apply plugin: 'com.android.library'
  apply plugin: 'kotlin-android'

  android {
    compileOptions {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }
    sourceSets {
      main.java.srcDirs += 'src/main/kotlin'
    }
  }
}

// Configure projects publishing options
subprojects { project ->
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.source
  }

  artifacts {
    archives androidSourcesJar
  }

  afterEvaluate {
    publishing {
      publications {
        maven(MavenPublication) {
          groupId = project.group
          artifactId = project.ext.artifactId
          version = project.version
          from components.release
          artifact androidSourcesJar

          pom {
            name = project.name
            description = 'LiveBundle React Native'
            url = 'https://github.com/electrode-io/livebundle/'
            licenses {
              license {
                name = 'The Apache Software License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              }
            }
            developers {
              developer {
                name = 'WalmartLabs'
              }
            }
            scm {
              connection = 'scm:git:https://github.com/electrode-io/livebundle.git'
              developerConnection = 'scm:git:ssh://git@github.com/electrode-io/livebundle.git'
              url = 'https://github.com/electrode-io/livebundle'
            }
          }
        }
      }

      repositories {
        maven {
          name = "mavencentral"
          url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
          credentials {
            username project.findProperty("repositoryUsername")
            password project.findProperty("repositoryPassword")
          }
        }
      }
    }

    signing {
      required { isReleaseVersion }
      sign publishing.publications.maven
    }
  }
}

